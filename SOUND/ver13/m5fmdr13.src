;********************************************************
;*		$$$FMDR.SRC	( SOUND RYUTHM CONTROL )*
;*  			ORG. M5FMDR13.SRC              	*
;*		'SOUND-SORCE'                           *
;*		 for Mega Drive (Z80)			*
;*			VER  1.31/1989.12.10		*
;*				BY        T.Uwabo       *
;********************************************************

	.LIST
	include m5eq13.lib
	include m5tb13.lib
	include m5mcr13.lib
	.XLIST

	;----------< rythm >---------------
rythm::
	CALL	COUNTCHK##
	call	Z,FNEXTDR		; call is need. for CMEND
	ret

;===========================;
; FM & PSG NEXT RYTHM DATA  ;  if lcont =1, then call
;===========================;
FNEXTDR1::
FNEXTDR::			
	LD	E,(IX+TBPON)		
	LD	D,(IX+TBPON+1)		; DE=TBLE POINTER
FNXDR1::
	LD	A,(DE)			; A = SOUND DATA. DE=TBPON
	INC	DE			; DE=(TBPON)+1
	CP	0E0H			; if data>0E0H ( command data)
	JP	NC,FNCOMDR		; then jump to command set.
	or	a
	jp	m,fm_drum_set

	dec	de			; table pointer -1
	ld	a,(ix+freqb)		; get rythm data
;=======================================;
;   RYTHM dammy headder set to work 	;
;=======================================;
;	a = rythm table data
fm_drum_set:
;	BIT	2,(IX+FLAG)		; disable flag on
;	JP	nz,rythm_l_chk		; then jump
	LD	(IX+FREQB),A
	CP	80H
	JP	Z,rythm_l_chk
	;---------------------
	PUSH	DE			; table pointer push
	ld	hl,RYTHM_WK
	bit	2,(hl)
	jr	nz,psg_rythm_set

	and	0fh			; bit 0,1,2,3 = FM rythm data
	jr	z,psg_rythm_set

	ex	af,af'
	CALL	KEYOFF_CHK##			; key off
	ex	af,af'
	;------------< SEND HEADDER TO RAM >-----------
	LD 	de,rythm_flg_tb
	ex	de,hl			; DE = rythm_wk
	ldi
	ldi
	ldi

	dec	a
	LD	HL,DRTB
;	CALL	SENDTB##		; HL = RYTHM HEADDER addr.
;	rst	sendtb_sub
	rst	gettbl_sub

	LD	BC,6
	LDIR
	CALL	STACK_SET##		; HL = next data point
fmdr_bias::
	ld	hl,RYTHM_WK+BIAS
	ld	a,(ix+bias)
	add	a,(hl)
	ld	(hl),a
TIMSETR::
	LD	A,(RYTHM_WK+FTIMB)
	LD	HL,DRVTB
;	CALL	SENDTB##
;	rst	sendtb_sub
	rst	gettbl_sub

	LD	a,(RYTHM_WK+VOLM)
	ld	e,(IX+VOLM)		; E = VOLM data store
	push	de
	add	a,e
	LD	(IX+VOLM),A
	CALL	LOADFMDATA##		; HL = VOICE TABLE TOP
	pop	de
	LD	(IX+VOLM),e		; E = VOLM data recover

	call	OPNINITIAL##		; se mode clear
psg_rythm_set::
	ld	hl,PSG_RYTHM_WK
	bit	2,(hl)
	jr	nz,psg_no_rythm
	LD	a,(IX+FREQB)		; A = table data
	and	70H			; bit 4,5,6  = PSG rythm data
	jr	z,psg_no_rythm		
	;-------< psg rythm work set >--------
	LD 	de,psg_rythm_flg_tb
	ex	de,hl			; DE = rythm_wk
	ldi
	ldi
	ldi
	srl	a
	srl	a
	srl	a
	srl	a			; high 4 bit ==> low 4 bit
	dec	a
	LD	HL,PSG_DRTB
;	CALL	SENDTB##		; HL = RYTHM HEADDER addr.
	rst	gettbl_sub
;	rst	sendtb_sub

	LD	BC,6
	LDIR
	CALL	STACK_SET##		; HL = next data point

psg_no_rythm:
	POP	DE			; table pointer get
	;-------------<< length set check >------
rythm_l_chk:
	ld	a,(de)			; next data get
	inc	de
	or	a
	jp	p,leng_set0##
	dec	de
	ld	a,(ix+ecstr)
	ld	(ix+lcont),a
	jp	flg_set##

rythm_flg_tb:
	db	80h,2,1
psg_rythm_flg_tb:
	DB	80H,0C0H,1
;=======================;
;	FNCOMDR		;  if data> 0DFH then COMMAND DATA 
;  RETHM COMMAND SET
;=======================;
FNCOMDR::
	LD	HL,FCOMDR1		; HL=return addr.
	JP	NECOMR##		;  jump to command routine
FCOMDR1::	
	INC	DE			; return from command
	JP	FNXDR1			; DE=TBPON


	;<<<<<<<<<<<<<<<<< RYTHM DATA TABLE >>>>>>>>>>>>>
PSG_DRTB:
	DW	P_HI,P_OHI

;<<<<<<<<< psg hihat table >>>>>>>>>
P_HI::
	DW	P_HITB
	DB	00,04h,00H,1			; bias,volm,vibr,enve
P_HITB::
	DB	CMNOIS,0E7H
	DB	0C2H,8,CMEND		; FREQ 014H


;<<<<<<<<< psg open hihat table >>>>>>>>>
P_OHI::
	DW	P_OHITB
	DB	00,06h,00H,2			; bias,volm,vibr,enve
P_OHITB::
	DB	CMNOIS,0E7H
	DB	0C5H,8,CMEND			; FREQ 017H


DRTB::
	;       S   B   H  
	DW	SNR,HTOM,MTOM,LTOM,BSD,HHT,LTOM2,SNR2,KSDS,HTH
DRVTB::
	DW	VSNR,VBD,VHHT,VSNR2,VSDSTB,VHIHTTB
;******************    DATA    ***********************
;************************
;   SNARE		*
;************************
SNR::
	DW	SNRT
	DB	00,ASNR,01H,0			; bias,volm,vibr,enve
SNRT::
	DB	FSNR,10H,CMEND

VSNR::			; VR,1  HT,B9H,MT,B8H, SNR,B4H
	CNF	6,7
	MD	0,6,0,3,0,3,0,3
	RSAR	0,25,0,31,0,31,0,31
	D1R	21,17,17,12
	D2R	16,10,6,9
	RRL	15,4,15,5,15,10,15,8
	TL	0,2,3,0

;************************
;  HI TOM		*
;************************
HTOM::
	DW	HTOMT
	DB	00,AHT,01H,0			; bias,volm,vibr,enve
HTOMT::
	DB	LRPAN,LSET,FHT,010,CMEND

;************************
;  MID TOM		*
;************************
MTOM::
	DW	MTOMT
	DB	00,AMT,01H,0			; bias,volm,vibr,enve
MTOMT::
	DB	FMT,010,CMEND

;************************
;  LOW TOM		*
;************************
LTOM::
	DW	LTOMT
	DB	00,ALT,01H,0			; bias,volm,vibr,enve
LTOMT::
	DB	LRPAN,RSET,FLT,010,CMEND

;************************
;  LOW TOM		*
;************************
LTOM2::
	DW	LTOMT2
	DB	00,ALT2,01H,0			; bias,volm,vibr,enve
LTOMT2::
	DB	FLT2,010,CMEND



;************************
;   BASS DR		*
;************************
BSD::
	DW	BSDT
	DB	00,ABDR,01H,1		; bias,volm,vibr,enve
BSDT::
	DB	FBDR,8,CMEND

VBD::	
	;---------------<BD >---------------
	CNF	2,7*2		; VR,1,TONE A6H
	MD	3,3,0,3,2,3,1,3
	RSAR	0,30,0,27,0,28,0,21
	D1R	22,18,23,16
	D2R	16,24,30,20
	RRL	15,4,15,5,15,4,15,4
	TL	8,0,16,0

	if	0

	;---------------<SNER TOM 2 >---------------
fev8403::			; VR,1 TONE B4H
	CNF	6,7
	MD	15,6,0,3,0,3,0,3
	RSAR	0,31,0,31,0,31,0,31
	D1R	13,17,17,12
	D2R	5,10,4,9
	RRL	112.10,15,5,15,10,15,8
	TL	00h,03h,04H,08H

	endif

;************************
;   close hi-hat	*
;************************
HHT::
	DW	HHTT
	DB	00,AHHT,01H,2		; bias,volm,vibr,enve
HHTT::
	DB	FHHT,016H,CMEND
;Hihat1
VHHT::
	;---------------<HIHAT >---------------
fev8401::			; bias 0, TONE,B0H
	CNF	2,7*2
	MD	14,9,11,5,2,4,2,2
	RSAR	2,22,2,22,2,30,2,22
	D1R	22,24,22,24
	D2R	16,23,17,24
	RRL	15,4,15,5,15,4,15,4
	TL	0,0,16,0
	

;************************
;   SNR 2		*
;************************
SNR2::
	DW	SNR2T
	DB	00,ASNR2,00H,3
SNR2T::
	DB	FSNR2,010H,CMEND
VSNR2::
	;---------------< SNR 2 >---------------
	CNF	4,7
	MD	15,0,0,0,0,0,0,0
	RSAR	0,31,0,26,0,24,0,28
	D1R	23,17,26,14
	D2R	0,15,20,16
	RRL	15,1,12,14,15,15,15,15
	TL	7,0,22,0

;**************************************************************************






;************************
;   Snare Drum    (S,H)	*
;************************
KSDS::
	DW	KSDST
	DB	0F7H,AKSDS,00H,4		; bias,volm,vibr,enve

KSDST:
 	DB	DTDR,3,0,0,0
	DB	FKSDS,20H
	DB	CMEND

	;Snare Drum 1 (Soft)
VSDSTB::
	CNF	4,7
	MD	10,0,0,5,0,7,0,0
	RSAR	0,31,0,23,0,25,0,29
	D1R	29,21,26,23
	D2R	6,24,7,25
	RRL	15,0,15,5,15,6,15,1
	TL	12,21,0,14

	if	0
	;	M1------  C1------  M2------  C2------
;	M0	020H,0FCH	;RL&FB&CONECT
;	M1	040H,00AH,050H,050H,048H,070H,058H,000H	;DT1&MUL
;	M3	080H,01FH,090H,017H,088H,019H,098H,01DH	;KS&AR
;	M4	0A0H,01DH,0B0H,015H,0A8H,01AH,0B8H,017H	;AMS-EN&D1R
;	M5	0C0H,000H,0D0H,050H,0C8H,054H,0D8H,010H	;DT2&D2R
;	M6	0E0H,006H,0F0H,058H,0E8H,067H,0F8H,019H	;D1L&RR
;	M2	060H,00CH,070H,015H,068H,000H,078H,00EH ;TL

	endif
;************************
;   OPEN HI HAT		*
;************************
HTH::
	DW	HTHT
	DB	00,AHTH,00H,7		; bias,volm,vibr,enve
HTHT::
	DB	DTDR,0,3,0,3
	DB	FHTH,08
	DB	CMEND
	
;Hihat1
VHIHTTB::
	CNF	5,7
	MD	0,0,15,0,15,0,15,0
	RSAR	0,31,2,31,2,31,2,31
	D1R	31,31,31,31
	D2R	0,14,16,15
	RRL	15,0,15,4,15,4,15,4
	TL	0,16,16,5
	;	M1------  C1------  M2------  C2------
;	DB2	FBC,0FDH-0C0H,RL,0C0H	;RL&FB&CONECT
;	DB4	MU1,00FH,MU2,00FH,MU3,00FH,MU4,00FH	;DT1&MUL
;	DB4	AR1,01FH,AR2,09FH,AR3,09FH,AR4,09FH	;KS&AR
;	DB4	DR1,01FH,DR2,01FH,DR3,01FH,DR4,01FH	;AMS-EN&D1R
;	DB4	SR1,000H,SR2,012H,SR3,010H,SR4,00FH	;DT2&D2R
;	DB4	RR1,005H,RR2,04FH,RR3,04FH,RR4,04FH	;D1L&RR
;	DB4	TL1,000H,TL2,010H,TL3,010H,TL4,005H	;TL

;**************************************************************************
